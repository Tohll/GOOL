
/*
 * ParserImportOutputTable.jjt
 */

/*----------------------------------------------------------------------------*/
/* The option used to the generation of a parser */

options {
}


/*----------------------------------------------------------------------------*/
/* The declaration of the input import table parser */

PARSER_BEGIN(ParserImportOutputTable)
package gool.generator.common.parseImport;
import java.io.*;
import java.util.ArrayList;
import java.util.List;
public class ParserImportOutputTable {
  public static void main(String[] args) throws ParseException {
  String chaine ="";
   try{
			InputStream ips=new FileInputStream("src/gool/testMatcher/test"); 
			InputStreamReader ipsr=new InputStreamReader(ips);
			BufferedReader br=new BufferedReader(ipsr);
			String ligne;
			while ((ligne=br.readLine())!=null){
				//System.out.println(ligne);
				chaine+=ligne+"\n";
			}
			br.close(); 
		}		
		catch (Exception e){
			System.out.println(e.toString());
		}
		
		ParserImportOutputTable parser = new ParserImportOutputTable(new java.io.StringReader(chaine));
    List<ImportWrapperOut> table = parser.parseTable();
    for(ImportWrapperOut wrap : table){
    	wrap.print(); 
    }
  }
}

PARSER_END(ParserImportOutputTable)


/*----------------------------------------------------------------------------*/
/* The tokens used to parse */

// Token skipped
SKIP : 
{     " " 
    | "\t" 
    | "\r" 
    | "\n"
    | "#" : IN_LINE_COMMENT
}

<IN_LINE_COMMENT> SKIP:
{
   "\n" : DEFAULT
}

<IN_LINE_COMMENT> MORE:
{
  < ~[] >
}

// Token about type
TOKEN :
{ 
 < ID: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* ( "." ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* )* >
| < STRING : ("L")? "\""
   ( ( ~["\"","\\","\n","\r"])
   | ("\\" (
             ["n","t","v","b","r","f","a","\\","?","'","\"","\n"]
            |
             "0" (["0"-"7"])*
            |
             ["1"-"9"] (["0"-"9"])*
            |
             ("0x" | "0X") (["0"-"9","a"-"f","A"-"F"])+
           )
     )
   )*
   "\"" >
}

/*----------------------------------------------------------------------------*/
/* The grammar used to parse */

// The root of the grammar
List<ImportWrapperOut> parseTable()  :
{List<ImportWrapperOut> wraps = new ArrayList<ImportWrapperOut>(); ImportWrapperOut wrap; }
{
    (wrap=decl_import_mapping() {wraps.add(wrap);})*  <EOF> {return wraps;}
}

// Used for all nodes declaration
ImportWrapperOut decl_import_mapping() :
{ImportWrapperOut wrap = new ImportWrapperOut();}
{
    gool_import(wrap) "<-" ouput_imports(wrap) 
     { return wrap;}
}

void gool_import(ImportWrapperOut wrap) :
{Token t; String s = "";}
{
    ("+" pathsWrapper_gool(wrap))? t=<ID> {
    wrap.setGoolImport(t.image);}	
}

void pathsWrapper_gool(ImportWrapperOut wrap) :
{}
{
	"[" list_paths_import_gool(wrap) "]"
}

void list_paths_import_gool(ImportWrapperOut wrap) :
{}
{
	pathWrapper_gool(wrap) ( LOOKAHEAD(2) "," pathWrapper_gool(wrap) )*
}

void pathWrapper_gool(ImportWrapperOut wrap) :
{Token t;}
{
	 t=<STRING>  {  wrap.addGoolIndirect(t.image.substring(1,t.image.length()-1)); }
}

void ouput_imports(ImportWrapperOut wrap) :
{Token t;}
{
	ouput_import(wrap) ( LOOKAHEAD(2) "," ouput_import(wrap) )*
}

void ouput_import(ImportWrapperOut wrap) :
{Token t;}
{
    t=<ID> {wrap.addInputImport(t.image);}	
}
